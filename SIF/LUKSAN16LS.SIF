***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LUKSAN16LS

*   Problem :
*   *********

*   Problem 16 (sparse exponential) in the paper

*     L. Luksan
*     Hybrid methods in large sparse nonlinear least squares
*     J. Optimization Theory & Applications 89(3) 575-595 (1996)

*   SIF input: Nick Gould, June 2017.

*   least-squares version

*   classification SUR2-AN-V-0

*  seed for dimensions

 IE S                   499

*  number of unknowns

 IM N         S         2
 IA N         N         2

*  number of equations

 IM M         S         4

*   Define useful parameters

 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4
 RE ONE                 1.0

*  data values

 RE Y1                  35.8
 RE Y2                  11.2
 RE Y3                  6.2
 RE Y4                  4.4

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        M
 XN E(I)
 ND

CONSTANTS

 IE K                   1
 DO J         1                        S
 Z  LUKSAN16  E(K)                     Y1
 IA K         K         1
 Z  LUKSAN16  E(K)                     Y2
 IA K         K         1
 Z  LUKSAN16  E(K)                     Y3
 IA K         K         1
 Z  LUKSAN16  E(K)                     Y4
 IA K         K         1
 ND

BOUNDS

 FR LUKSAN16  'DEFAULT'

START POINT

 DO I         1                        N
 DI I         4
 XV LUKSAN16  X(I)       -0.8
 ND

 DO I         2                        N
 DI I         4
 XV LUKSAN16  X(I)       1.2
 ND

 DO I         3                        N
 DI I         4
 XV LUKSAN16  X(I)       -1.2
 ND

 DO I         4                        N
 DI I         4
 XV LUKSAN16  X(I)       0.8
 ND

ELEMENT TYPE

 EV EXPSUM    X1
 EV EXPSUM    X2
 EV EXPSUM    X3
 EV EXPSUM    X4
 IV EXPSUM    S
 EP EXPSUM    P2OL
 EP EXPSUM    PLI

ELEMENT USES

 XT 'DEFAULT' EXPSUM

 DO P         1                        3
 RI RP        P
 R* RP2       RP                       RP
 IE K                   1
 IE I                   0
 DO J         1                        S
 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3
 IA I+4       I         4
 DO L         1                        4
 RI RL        L
 R/ P2OL      RP2                      RL
 R* PL        RP                       RL
 R/ PLI       ONE                      PL
 ZV E(K,P)    X1                       X(I+1)
 ZV E(K,P)    X2                       X(I+2)
 ZV E(K,P)    X3                       X(I+3)
 ZV E(K,P)    X4                       X(I+4)
 ZP E(K,P)    P2OL                     P2OL
 ZP E(K,P)    PLI                      PLI
 IA K         K         1
 OD L
 IA I         I         2
 OD J
 OD P

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

 DO K         1                        M
 XE E(K)      E(K,1)                   E(K,2)
 XE E(K)      E(K,3)
 ND

OBJECT BOUND

 LO LUKSAN16             0.0

*   Solution

*LO SOLTN                0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LUKSAN16LS

TEMPORARIES

 I  EXP
 R  EXPARG

INDIVIDUALS

 T  EXPSUM
 R  S         X1        1.0            X2        2.0
 R  S         X3        3.0            X4        4.0

 A  EXPARG              P2OL * EXP( PLI * S )
 F                      EXPARG
 G  S                   PLI * EXPARG
 H  S         S         PLI * PLI * EXPARG

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        LUKSAN16LS

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
