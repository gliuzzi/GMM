***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LUKSAN15LS

*   Problem :
*   *********

*   Problem 15 (sparse signomial) in the paper

*     L. Luksan
*     Hybrid methods in large sparse nonlinear least squares
*     J. Optimization Theory & Applications 89(3) 575-595 (1996)

*   SIF input: Nick Gould, June 2017.

*   least-squares version

*   classification SUR2-AN-V-0

*  seed for dimensions

 IE S                   499

*  number of unknowns

 IM N         S         2
 IA N         N         2

*  number of equations

 IM M         S         4

*   Define useful parameters

 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4
 RE ONE                 1.0

*  data values

 RE Y1                  35.8
 RE Y2                  11.2
 RE Y3                  6.2
 RE Y4                  4.4

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        M
 XN E(I)
 ND

CONSTANTS

 IE K                   1
 DO J         1                        S
 Z  LUKSAN15  E(K)                     Y1
 IA K         K         1
 Z  LUKSAN15  E(K)                     Y2
 IA K         K         1
 Z  LUKSAN15  E(K)                     Y3
 IA K         K         1
 Z  LUKSAN15  E(K)                     Y4
 IA K         K         1
 ND

BOUNDS

 FR LUKSAN15  'DEFAULT'

START POINT

 DO I         1                        N
 DI I         4
 XV LUKSAN15  X(I)       -0.8
 ND

 DO I         2                        N
 DI I         4
 XV LUKSAN15  X(I)       1.2
 ND

 DO I         3                        N
 DI I         4
 XV LUKSAN15  X(I)       -1.2
 ND

 DO I         4                        N
 DI I         4
 XV LUKSAN15  X(I)       0.8
 ND

ELEMENT TYPE

 EV SIGNOM    X1
 EV SIGNOM    X2
 EV SIGNOM    X3
 EV SIGNOM    X4
 EP SIGNOM    P2OL
 EP SIGNOM    PLI

ELEMENT USES

 XT 'DEFAULT' SIGNOM

 DO P         1                        3
 RI RP        P
 R* RP2       RP                       RP
 IE K                   1
 IE I                   0
 DO J         1                        S
 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3
 IA I+4       I         4
 DO L         1                        4
 RI RL        L
 R/ P2OL      RP2                      RL
 R* PL        RP                       RL
 R/ PLI       ONE                      PL
 ZV E(K,P)    X1                       X(I+1)
 ZV E(K,P)    X2                       X(I+2)
 ZV E(K,P)    X3                       X(I+3)
 ZV E(K,P)    X4                       X(I+4)
 ZP E(K,P)    P2OL                     P2OL
 ZP E(K,P)    PLI                      PLI
 IA K         K         1
 OD L
 IA I         I         2
 OD J
 OD P


GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

 DO K         1                        M
 XE E(K)      E(K,1)                   E(K,2)
 XE E(K)      E(K,3)
 ND

OBJECT BOUND

 LO LUKSAN15             0.0

*   Solution

*LO SOLTN                0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LUKSAN15LS

TEMPORARIES

 R  P
 R  SIGNP
 R  PX1
 R  PX2
 R  PX3
 R  PX4
 R  PX1X1
 R  PX1X2
 R  PX1X3
 R  PX1X4
 R  PX2X2
 R  PX2X3
 R  PX2X4
 R  PX3X3
 R  PX3X4
 R  PX4X4
 R  F
 R  G
 R  H
 L  PPOS

INDIVIDUALS

 T  SIGNOM
 A  P                   X1 * ( X2 ** 2 ) * ( X3 ** 3 )
 A+                        * ( X4 ** 4 )
 A  PPOS                P > 0.0D0
 I  PPOS      SIGNP     1.0D0
 E  PPOS      SIGNP     - 1.0D0
 A  P                   P * SIGNP
 A  PX1                 ( X2 ** 2 ) * ( X3 ** 3 ) * ( X4 ** 4 )
 A  PX2                 X1 * ( 2.0D0 * X2 ) * ( X3 ** 3 )
 A+                        * ( X4 ** 4 )
 A  PX3                 X1 * ( X2 ** 2 ) * ( 3.0D0 * X3 ** 2 )
 A+                        * ( X4 ** 4 )
 A  PX4                 X1 * ( X2 ** 2 ) * ( X3 ** 3 )
 A+                        * ( 4.0D0 * X4 ** 3 )

 A  PX1X1               0.0D0
 A  PX1X2               ( 2.0D0 * X2 ) * ( X3 ** 3 )
 A+                       * ( X4 ** 4 )
 A  PX1X3               ( X2 ** 2 ) * ( 3.0D0 * X3 ** 2 )
 A+                       * ( X4 ** 4 )
 A  PX1X4               ( X2 ** 2 ) * ( X3 ** 3 )
 A+                       * ( 4.0D0 * X4 ** 3 )
 A  PX2X2               X1 * ( 2.0D0 ) * ( X3 ** 3 )
 A+                        * ( X4 ** 4 )
 A  PX2X3               X1 * ( 2.0D0 * X2 ) * ( 3.0D0 * X3 ** 2 )
 A+                        * ( X4 ** 4 )
 A  PX2X4               X1 * ( 2.0D0 * X2 ) * ( X3 ** 3 )
 A+                        * ( 4.0D0 * X4 ** 3 )
 A  PX3X3               X1 * ( X2 ** 2 ) * ( 6.0D0 * X3 )
 A+                        * ( X4 ** 4 )
 A  PX3X4               X1 * ( X2 ** 2 ) * ( 3.0D0 * X3 ** 2 )
 A+                        * ( 4.0D0 * X4 ** 3 )
 A  PX4X4               X1 * ( X2 ** 2 ) * ( X3 ** 3 )
 A+                        * ( 12.0D0 * X4 ** 2 )

 A  F                   P2OL * P ** PLI
 A  G                   P2OL * PLI * P ** ( PLI - 1.0D0 )
 A  H                   P2OL * PLI * ( PLI - 1.0D0 )
 A+                      * P ** ( PLI - 2.0D0 )

 F                      F
 G  X1                  G * PX1
 G  X2                  G * PX2
 G  X3                  G * PX3
 G  X4                  G * PX4
 H  X1        X1        H * PX1 * PX1 + G * PX1X1
 H  X1        X2        H * PX1 * PX2 + G * PX1X2
 H  X1        X3        H * PX1 * PX3 + G * PX1X3
 H  X1        X4        H * PX1 * PX4 + G * PX1X4
 H  X2        X2        H * PX2 * PX2 + G * PX2X2
 H  X2        X3        H * PX2 * PX3 + G * PX2X3
 H  X2        X4        H * PX2 * PX4 + G * PX2X4
 H  X3        X3        H * PX3 * PX3 + G * PX3X3
 H  X3        X4        H * PX3 * PX4 + G * PX3X4
 H  X4        X4        H * PX4 * PX4 + G * PX4X4

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        LUKSAN15LS

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
