***************************
* SET UP THE INITIAL DATA *
***************************

NAME          BOXPOWER

*   Problem :
*   *********

*   Source:
*   
*   function with a "box-shapped" singular Hessian

*   SIF input: Nick Gould, June 2013

*   classification OUR2-AN-V-0

*   Number of variables

*IE N                   10             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
 IE N                   10000          $-PARAMETER     original value
 IE N                   20000          $-PARAMETER
*IE N                   10             $-PARAMETER

*  Singularity type p leading to term x^2(p+1)

 IE P                   9

*   Other parameters

 IE 1                   1
 IE 2                   2
 IA N-1       N         -1
 IA P+1       P         1
 IM 2P+2      P+1       2
 RI POWER     2P+2

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 XN G(1)      X(1)      1.0
 DO I         2                        N-1
 XN G(I)      X(1)      1.0            X(I)      1.0
 XN G(I)      X(N)      1.0
 ND
 XN G(N)      X(N)      1.0

BOUNDS

 FR BOXPOWER  'DEFAULT'

START POINT

 XV BOXPOWER  'DEFAULT' 0.99

GROUP TYPE

 GV L2        GVAR
 GV POWER     GVAR
 GP POWER     P

GROUP USES

 DO I         1                        N-1
 XT G(I)      L2
 ND

 XT G(N)      POWER
 ZP G(N)      P                        POWER

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO POWER               0.0

*   Solution

*LO SOLTN               0.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        BOXPOWER

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

 T  POWER
 F                      GVAR ** P
 G                      P * GVAR ** ( P - 1.0 )
 H                      P * ( P - 1.0 ) * GVAR ** ( P - 2.0 )

ENDATA
