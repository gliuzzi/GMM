***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ARGLINC

*   Problem :
*   *********
*   Variable dimension rank one linear problem, with zero rows and columns

*   Source: Problem 34 in
*   J.J. More', B.S. Garbow and K.E. Hillstrom,
*   "Testing Unconstrained Optimization Software",
*   ACM Transactions on Mathematical Software, vol. 7(1), pp. 17-41, 1981.

*   See also Buckley#101 (with different N and M)
*   SIF input: Ph. Toint, Dec 1989.

*   classification SUR2-AN-V-0

*   This problem is a linear least squares

*   N is the number of free variables
*   M is the number of equations ( M.ge.N)

*IE N                   10             $-PARAMETER
*IE N                   50             $-PARAMETER 
*IE N                   100            $-PARAMETER
 IE N                   200            $-PARAMETER

*IE M                   20             $-PARAMETER .ge. N
*IE M                   100            $-PARAMETER .ge. N
*IE M                   200            $-PARAMETER .ge. N
 IE M                   400            $-PARAMETER .ge. N

*   Define useful parameters

 IE 1                   1
 IE 2                   2

 IA M-1       M         -1
 IA N-1       N         -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 XN G(1)

 DO I         2                        M-1
 RI RI        I
 RA I-1       RI        -1.0
 DO J         2                        N-1
 RI RJ        J
 R* JI-1      I-1                      RJ
 ZN G(I)      X(J)                     JI-1
 ND

 XN G(M)

CONSTANTS

 X  ARGLINC   'DEFAULT' 1.0

BOUNDS

 FR ARGLINC   'DEFAULT'

START POINT

 V  ARGLINC   'DEFAULT' 1.0

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

OBJECT BOUND

*   Solution

*LO SOLTN(10)           6.13513513
*LO SOLTN(50)           26.1269035
*LO SOLTN(100)          26.1269

ENDATA



*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        ARGLINC

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
